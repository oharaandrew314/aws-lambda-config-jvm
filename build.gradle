ext {
    artifact_version = '0.1.2'
    artifact_id = 'aws-lambda-config-jvm'
    artifact_group = 'io.andrewohara'
    artifact_description = 'A simple configuration client for AWS serverless JVM systems, written in Kotlin'
}

buildscript {
    ext.kotlin_version = '1.1.4-3'

    repositories {
        jcenter()
        dependencies {
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'

group artifact_group
version artifact_version

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'com.amazonaws:aws-java-sdk-ssm:1.11.224'

    testCompile 'junit:junit:4.7'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
    testCompile "com.github.tomakehurst:wiremock:2.10.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

publishing {
    publications {
        mavenPub(MavenPublication) {
            from components.java
            groupId artifact_group
            artifactId artifact_id
            version artifact_version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenPub']
    pkg {
        repo = 'maven-public'
        name = artifact_id
        description = artifact_description
        licenses = ['MIT']
        vcsUrl = 'https://github.com/oharaandrew314/aws-lambda-config-jvm.git'
        websiteUrl = 'https://github.com/oharaandrew314/aws-lambda-config-jvm'
        issueTrackerUrl = 'https://github.com/oharaandrew314/aws-lambda-config-jvm/issues'
        publicDownloadNumbers = true
        labels=['aws', 'lambda', 'serverless']
        version {
            name = artifact_version
            desc = 'A simple configuration client for AWS serverless JVM systems, written in Kotlin.'
            released = new Date()
            vcsTag = artifact_version
            publish = true
        }
    }
}

test {
    testLogging {
        events "failed"
        exceptionFormat "short"
    }
}


jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport